apply plugin: 'com.android.application'
//apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'        // apply dexcount AFTER the Android plugin!
//apply plugin: 'com.neenbedankt.android-apt'     // android-apt modifies the javaCompile task, apply this plugin LAST!

apply from: '../quality/checkstyle.gradle'
apply from: '../quality/findbugs.gradle'
apply from: '../quality/pmd.gradle'
apply from: '../quality/jacoco.gradle'

def isOnCi() {
    def ci = System.getenv("CI")
    return (ci != null && ci == "true")
}

android {
    compileSdkVersion 25
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "me.maxdev.popularmoviesapp"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 3
        versionName "2.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def STRING = "String"
    def UNDEFINED = '"UNDEFINED"'
    def THE_MOVIE_DB_API_KEY = "THE_MOVIE_DB_API_KEY"

    buildTypes {
        debug {
            versionNameSuffix "-debug"
            minifyEnabled false
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro',
                    'proguard-glide.pro',
                    'proguard-retrofit.pro',
                    'proguard-rxjava.pro'
        }
    }
    buildTypes.each {
        if (!isOnCi()) {
            it.buildConfigField STRING, THE_MOVIE_DB_API_KEY, MY_MOVIE_DB_API_KEY
        } else {
            it.buildConfigField STRING, THE_MOVIE_DB_API_KEY, UNDEFINED
        }
    }

    lintOptions {
        disable 'InvalidPackage' // Okio references java.nio that does not presented in Android SDK
        disable 'GoogleAppIndexingWarning' // App does not have deep linking
        disable 'IconDipSize'
        warningsAsErrors true
        abortOnError true
        textReport true
        textOutput 'stdout'
    }

    // Log unit tests.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            exceptionFormat 'full'
        }
        jacoco {
            includeNoLocationClasses = true
        }
    }

    // Log instrumentation tests.
    tasks.withType(com.android.build.gradle.internal.tasks.AndroidTestTask) { task ->
        task.doFirst {
            logging.level = LogLevel.INFO
        }
        task.doLast {
            logging.level = LogLevel.LIFECYCLE
        }
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()

if (keystorePropertiesFile.exists()) {

    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    if (keystoreProperties != null &&
            keystoreProperties.containsKey('storeFile') &&
            keystoreProperties.containsKey('storePassword') &&
            keystoreProperties.containsKey('keyAlias') &&
            keystoreProperties.containsKey('keyPassword')) {

        println 'keystore.properties found. Sign APK'
        android.signingConfigs.config.storeFile file(keystoreProperties['storeFile'])
        android.signingConfigs.config.storePassword keystoreProperties['storePassword']
        android.signingConfigs.config.keyAlias keystoreProperties['keyAlias']
        android.signingConfigs.config.keyPassword keystoreProperties['keyPassword']

    } else {
        println 'keystore.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'keystore.properties not found'
    android.buildTypes.release.signingConfig = null
}

ext {
    supportLibraryVersion = "25.0.1"
    retrofitVersion = "2.1.0"
    okHttpVersion = "3.4.2"
    rxJavaVersion = "1.2.2"
    rxAndroidVersion = "1.2.1"
    rxLifecycleVersion = "1.0"
    rxBindingsVersion = "0.4.0"
    butterknifeVersion = "8.4.0"
    daggerVersion = "2.7"
    glideVersion = "3.7.0"
    jUnitVersion = "4.12"
    mockitoVersion = "1.10.19"
    testingSupportLibraryVersion = "0.5"
    exoPlayerVersion = "2.6.1"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //Thư viện hỗ trợ viết giao diện
    implementation "com.android.support:appcompat-v7:${supportLibraryVersion}"
    implementation "com.android.support:design:${supportLibraryVersion}"
    implementation "com.android.support:cardview-v7:${supportLibraryVersion}"
    implementation "com.android.support:support-annotations:${supportLibraryVersion}"

    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    implementation "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"

    implementation "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"

    implementation "io.reactivex:rxjava:${rxJavaVersion}"
    implementation "io.reactivex:rxandroid:${rxAndroidVersion}"

    //Thư viện play video, phát triển bởi Google(open source)
    implementation "com.google.android.exoplayer:exoplayer:${exoPlayerVersion}"
    implementation 'com.google.android.exoplayer:exoplayer-core:2.X.X'
    implementation 'com.google.android.exoplayer:exoplayer-dash:2.X.X'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.X.X'

    implementation "com.trello:rxlifecycle:${rxLifecycleVersion}"
    implementation "com.trello:rxlifecycle-android:${rxLifecycleVersion}"
    implementation "com.trello:rxlifecycle-components:${rxLifecycleVersion}"

    implementation "com.jakewharton.rxbinding:rxbinding-appcompat-v7:${rxBindingsVersion}"

    implementation "com.jakewharton:butterknife:${butterknifeVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknifeVersion}"

    implementation "com.google.dagger:dagger:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    //Thư viện load ảnh
    implementation "com.github.bumptech.glide:glide:${glideVersion}"

    //Thư viện để viết unit test
    testImplementation "junit:junit:${jUnitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"

    androidTestImplementation "com.android.support:support-annotations:${supportLibraryVersion}"
    androidTestImplementation "com.android.support.test:runner:${testingSupportLibraryVersion}"
    androidTestImplementation "com.android.support.test:rules:${testingSupportLibraryVersion}"
}
